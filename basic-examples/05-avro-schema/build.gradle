plugins {
  id 'io.atomicwire.gradle.orchestra'
}

/*

This example demonstrates generating an Avro schema for a custom Orchestra spec.

Run `./gradlew :basic-examples:05-avro:printAvroSchemaLocation` to print the location where the generated Avro schema
can be found.

 */

orchestra {
  specification {
    // By default, the plugin looks for your Markdown file at `orchestra/specification/<project-name>.md`
    markdown {}

    // In order to generate an Avro schema for an Orchestra spec, additional type information is required that indicates
    // which Avro datatype corresponds to each datatype in the Orchestra spec.
    enrich {
      typeInfoFile = layout.projectDirectory.file('orchestra/specification/type-info.yml')
    }
  }

  avro {
    namespace = 'io.atomicwire.orchestra.example'
  }
}

def printAvroSchemaLocation = tasks.register('printAvroSchemaLocation') {
  dependsOn tasks.named('orchestraGenerateAvroSchema')

  doLast {
    println "Generated Avro schemas in ${orchestra.avro.schemaOutputDir.get()}"
  }
}

tasks.named('build') {
  dependsOn printAvroSchemaLocation
}
