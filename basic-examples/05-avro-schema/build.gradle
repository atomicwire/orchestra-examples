plugins {
  id 'io.atomicwire.gradle.orchestra'
}

orchestra {
  specification {
    // By default, the plugin looks for your Markdown file at `orchestra/specification/<project-name>.md`
    markdown {}

    // In order to generate an Avro schema for an Orchestra spec, additional type information is required that indicates
    // which Avro datatype corresponds to each datatype in the Orchestra spec.
    encoding {
      datatypeMapping([
        double: [
          Avro: 'double',
        ],
        string: [
          Avro: 'string',
        ],
        timestamp: [
          Avro: [
            standardType: 'long',
            avroLogicalType: 'timestamp-micros',
          ],
        ],
      ])
    }
  }

  avro {
    namespace = 'org.example.orchestra'
  }
}

tasks.named('runExample') {
  dependsOn tasks.named('orchestraGenerateAvroSchema')

  doLast {
    println "Generated Avro schemas in ${orchestra.avro.schemaOutputDir.get()}"
  }
}
